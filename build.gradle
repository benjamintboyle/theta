import com.github.spotbugs.SpotBugsTask

plugins {
    id 'java'
    id 'checkstyle'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'org.springframework.boot' version '2.2.6.RELEASE'
    id 'com.github.spotbugs' version '3.0.0'
}

repositories {
    mavenCentral()
}

sourceCompatibility = 14
targetCompatibility = 14

springBoot {
    buildInfo {
        properties {
            artifact = 'theta-artifact'
            group = 'theta-group'
            name = 'theta-project'
            version = '0.4'
        }
    }
}

dependencies {
    // Production code dependencies
    implementation('org.springframework.boot:spring-boot-starter-webflux')
    implementation('org.springframework.boot:spring-boot-starter-actuator')

    implementation files('libs/interactive-brokers-api-9.72.18.jar')

    // Test code dependencies
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation('io.projectreactor:reactor-test')
}

test {
    useJUnitPlatform {
        // includeTags 'integration', 'feature-168'
        // excludeTags 'integration'
    }

    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: Runtime.runtime.availableProcessors()
}

wrapper {
    gradleVersion = '6.4.1'
}

checkstyle {
    toolVersion '8.27'
    configFile file("config/checkstyle/google_checks.xml")
    // configFile file("${configDirectory}/google_checks.xml")
}

spotbugs {
    ignoreFailures = false
    effort = 'max'
    reportLevel = 'low'
}

// To generate an HTML report instead of XML
tasks.withType(SpotBugsTask) {
    reports.xml.enabled = false
    reports.html.enabled = true
}
