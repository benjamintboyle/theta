plugins {
    id 'java'
    id 'checkstyle'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'org.springframework.boot' version '2.2.6.RELEASE'
    id 'com.github.spotbugs' version '3.0.0'
}

repositories {
    mavenCentral()
}

sourceCompatibility = 13
targetCompatibility = 13

bootJar {
    archiveBaseName = 'theta'
    archiveVersion = '0.4'
}

dependencies {
    // Production code dependencies
    implementation('org.springframework.boot:spring-boot-starter-webflux')
    implementation('org.springframework.boot:spring-boot-starter-actuator')

    implementation files('libs/interactive-brokers-api-9.72.18.jar')

    // Test code dependencies
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

test {
    useJUnitPlatform {
        // includeTags 'integration', 'feature-168'
        excludeTags 'integration'
    }

    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: Runtime.runtime.availableProcessors()
}

wrapper {
    gradleVersion = '6.4'
}

checkstyle {
    toolVersion '8.27'
    configFile file("config/checkstyle/google_checks.xml")
    // configFile file("${configDirectory}/google_checks.xml")
}

spotbugs {
    ignoreFailures = false
    effort = 'max'
    reportLevel = 'low'
}

// To generate an HTML report instead of XML
tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports.xml.enabled = false
    reports.html.enabled = true
}
